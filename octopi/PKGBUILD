# Maintainer: Condres OS team <info@codelinsoft.it>

# Default notifier is use simply qt5
# To use with Knotifications support, uncomment line below
USE_NOTIFIER='1'

# Default source is the latest release tarball
# To use a specified git version, uncomment line below and set to the git commit hash
#USE_SOURCE='251822c88747c216c6ebb9a342a80097ee744117'

# Default build with qtermwidget support
# To disable, uncomment line below
#DISABLE_QTERMWIDGET='1'

# Default build with pikaur support
# To disable, uncomment line below
#DISABLE_PIKAUR='1'


pkgbase=octopi
pkgname=('octopi' 'octopi-repoeditor' 'octopi-pacmanhelper' 'octopi-cachecleaner'
         'octopi-notifier')
pkgver=0.9.0
pkgrel=12
# This is the release package so the below _gitcommit variable should (usually) be commented out.
#_gitcommit=
arch=('i686' 'x86_64')
url="http://octopiproject.wordpress.com"
license=('GPL2')
makedepends=('qt5-declarative' 'knotifications' 'libnotify' 'alpm_octopi_utils' 'clang' 'qtermwidget')

  source=("octopi-${pkgver}.tar.gz::https://github.com/aarnt/octopi/archive/v${pkgver}.tar.gz")

source+=('remove-qdialog.patch')
sha256sums=('f6c7fe6bbcc26a79b1e455a42a8dc906bee4edd1b386b4f76178f006f4d7f822'
            'af8ebc8892a37c866556df391a078a50c6c56a4425e7ef31d35b27871e490229'
            )


prepare() {
  
    cd "${srcdir}/${pkgbase}-${pkgver}"
    

  # sed version
  sed -i -e "s|0.9.0|${pkgver}-${pkgrel}|g" src/strconstants.cpp

  # fix network detection
  patch -p1 -i $srcdir/remove-qdialog.patch
  cp -r notifier notifier-qt5
  cp -r notifier notifier-frameworks
  sed -i 's|#KSTATUS|KSTATUS|' notifier-frameworks/octopi-notifier/octopi-notifier.pro
}

build() {
  cpucount=$(grep -c processor /proc/cpuinfo 2>/dev/null)
  jc=$((${cpucount:-1}))

  
    cd "${srcdir}/${pkgbase}-${pkgver}"
 

  msg "Starting build..."
  qmake-qt5 -makefile octopi.pro -spec linux-clang
  make -j $jc

  cd notifier/pacmanhelper
  msg "Building pacmanhelper..."
  qmake-qt5 -makefile pacmanhelper.pro -spec linux-clang
  make -j $jc

  cd ../../notifier-qt5/octopi-notifier
  msg "Building octopi-notifier-qt5..."
  qmake-qt5 -makefile octopi-notifier.pro -spec linux-clang
  make -j $jc

 

  cd ../../repoeditor
  msg "Building octopi-repoeditor..."
  qmake-qt5 -makefile octopi-repoeditor.pro -spec linux-clang
  make -j $jc

  cd ../cachecleaner
  msg "Building octopi-cachecleaner..."
  qmake-qt5 -makefile octopi-cachecleaner.pro -spec linux-clang
  make -j $jc
}

package_octopi() {
  pkgdesc="A powerful Pacman frontend using Qt5 libs"
  depends=('qt5-base' 'qt5-declarative' 'xterm' 'pacman' 'alpm_octopi_utils>=1.0.0-2' 'qtermwidget' 'pacman-contrib')
  optdepends=('kdesu: for KDE'
              'gksu: for XFCE, Gnome, LXDE, Cinnamon'
              'lxqt-sudo: for LXQT'
              'gnome-keyring: for password management'
              'gist: for SysInfo report'
              'pacaur: for AUR support'
              'trizen: for AUR support'
              'yaourt: for AUR support'
              'octopi-repoeditor: for editing functions'
              'octopi-cachecleaner: for cleaning functions'
              'octopi-notifier-qt5: for notifications'
              'octopi-notifier-frameworks: for notifications with Knotifications support'
              'pacmanlogviewer: to view pacman log files')

  
    cd "${srcdir}/${pkgbase}-${pkgver}"
  

  install -Dm755 "bin/octopi" "${pkgdir}/usr/bin/octopi"
  install -Dm644 "octopi.desktop" "${pkgdir}/usr/share/applications/octopi.desktop"
  install -Dm644 "resources/images/octopi_green.png" "${pkgdir}/usr/share/icons/octopi.png"
  install -Dm644 "resources/images/octopi_green.png" "${pkgdir}/usr/share/icons/hicolor/48x48/apps/octopi.png"
  install -Dm644 "resources/images/octopi_red.png" "${pkgdir}/usr/share/icons/octopi_red.png"
  install -Dm644 "resources/images/octopi_yellow.png" "${pkgdir}/usr/share/icons/octopi_yellow.png"

  install -D -m755 speedup/speedup-octopi.sh ${pkgdir}/usr/bin/speedup-octopi.sh
  install -D -m644 speedup/octopi.service ${pkgdir}/etc/systemd/system/octopi.service
}

package_octopi-pacmanhelper() {
  pkgdesc="Pacman helper for Octopi notifier"
  depends=('qt5-base')

  
    cd "${srcdir}/${pkgbase}-${pkgver}"
  

  install -Dm755 "notifier/bin/pacmanhelper" "${pkgdir}/usr/lib/octopi/pacmanhelper"
  install -Dm644 "notifier/pacmanhelper/polkit/org.octopi.pacman.policy" "${pkgdir}/usr/share/polkit-1/actions/org.octopi.pacman.policy"
  install -Dm644 "notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.conf" "${pkgdir}/etc/dbus-1/system.d/org.octopi.pacmanhelper.conf"
  install -Dm644 "notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.xml" "${pkgdir}/usr/share/dbus-1/interfaces/org.octopi.pacmanhelper.xml"
  install -Dm644 "notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.service" "${pkgdir}/usr/share/dbus-1/system-services/org.octopi.pacmanhelper.service"
}

package_octopi-repoeditor() {
  pkgdesc="Repoeditor for Octopi"
  depends=('qt5-base')

  
    cd "${srcdir}/${pkgbase}-${pkgver}"
  

  install -D -m755 "repoeditor/bin/octopi-repoeditor" "${pkgdir}/usr/bin/octopi-repoeditor"
}

package_octopi-cachecleaner() {
  pkgdesc="Cachecleaner for Octopi"
  depends=('qt5-base')

  
    cd "${srcdir}/${pkgbase}-${pkgver}"
  

  install -D -m755 "cachecleaner/bin/octopi-cachecleaner" "${pkgdir}/usr/bin/octopi-cachecleaner"
  install -D -m644 "cachecleaner/octopi-cachecleaner.desktop" "${pkgdir}/usr/share/applications/octopi-cachecleaner.desktop"
}

package_octopi-notifier() {
  pkgdesc="Notifier for Octopi using Qt5 libs"
  depends=('octopi-pacmanhelper' 'libnotify' 'qt5-base' 'alpm_octopi_utils>=1.0.0-2')
  optdepends=('octopi: launch graphical package manager from tray'
              'xfce4-notifyd: for notifications in XFCE')
  conflicts=('octopi-notifier')
  provides=("octopi-notifier=${pkgver}")
  replaces=('octopi-qt5-notifier' 'octopi-notifier-qt4' 'octopi-notifier-qt5')


    cd "${srcdir}/${pkgbase}-${pkgver}"
  

  # Octopi-notifier-qt5 files
  install -Dm755 "notifier-qt5/bin/octopi-notifier" "${pkgdir}/usr/bin/octopi-notifier"
  install -Dm644 "notifier-qt5/octopi-notifier/octopi-notifier.desktop" "${pkgdir}/etc/xdg/autostart/octopi-notifier.desktop"
}


