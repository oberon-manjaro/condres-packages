# Maintainer: Condres OS team <info@codelinsoft.it>

pkgname=lumina-desktop
_patch=-p1
pkgver=1.4.0
pkgrel=3
pkgdesc="A Lightweight QT5 Desktop for FreeBSD"
arch=('x86_64' 'i686')
url="https://github.com/trueos/lumina"
license=('BSD')
depends=('qt5-base' 'qt5-svg' 'qt5-multimedia' 'qt5-x11extras' 'fluxbox'
        'breeze-icons' 'xscreensaver' 'desktop-file-utils' 'poppler-qt5')
optdepends=('xorg-xbacklight: required for changing screen brightness'
            'alsa-utils: required for adjusting audio volume'
            'acpi: required for monitoring battery life'
            'numlockx: required for changign state of numlock at login'
            'pavucontrol: required for detatched audio mixer'
            'fluxmod-styles: A good set of Fluxbox themes to improve the appearence of window decorations'
            )
makedepends=('qt5-tools')
options=('!makeflags')
conflicts=("${pkgname}-git" "lumina-de-git")
source=("${pkgname}-${pkgver}-${pkgrel}.tar.gz::${url}/archive/v${pkgver}${_patch}.tar.gz")
md5sums=('d82e6d960ecd4f1db1eb4637e73b9eec')


build(){
    cd $srcdir/lumina-${pkgver}${_patch}
    qmake-qt5 QMAKE_CFLAGS_ISYSTEM= PREFIX="/usr" QT5LIBDIR=/usr/lib/qt
    make
}

package() {
    cd $srcdir/lumina-${pkgver}${_patch}
    make INSTALL_ROOT="${pkgdir}" install
    mv "${pkgdir}"/usr/etc "${pkgdir}"/etc
}
 
