# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: maz-1 <loveayawaka@gmail.com>
# Maintainer: Condres OS Dev Team (x86_64) <info@codelinsoft.it>
pkgname=ndiswrapper-condres
pkgver=1.61
_extramodules=extramodules-Condres
pkgrel=25
pkgdesc="Module for NDIS (Windows Network Drivers) drivers supplied by vendors, kernel module for Arch kernel"
arch=('x86_64')
url="http://sourceforge.net/projects/ndiswrapper/"
license=('GPL')
depends=('linux')
makedepends=('linux-headers')
provides=('NDISWRAPPER-MODULE')
replaces=('ndiswrapper-module')
source=("ndiswrapper-$pkgver.tar.gz::https://github.com/pgiri/ndiswrapper/archive/v$pkgver.tar.gz"
        'linux-4.11.patch' 'linux-4.13.patch' 'linux-4.15.patch' 'linux-5.0.patch')
sha512sums=('SKIP'
            '5350308406457658fb31c378d6f3dd2a34c7331aebe5bdc80744bf955f1144e88181441704ccc7532f55d6fbe8d410440fb8838c2955d688dde4b55f74fc6ba5'
            'de026a271cbfbefee99bc00a13c50be87294e5e2c5407d23cff6ce2d2a0c853542ce4f7e3e150741c7e8e32466fb1ab7cab266957da5bcca8376fb0f38e94482'
            '52bd3d2025ffb8a3f6a4a1a1fee1b87d3811f30a210a54aaa7b37838eb256cd766bc1aeda24e4b4f4829992584931ecd97a042794c9b854bd1ebbf452dce2abf'
            '4918dcebd62ae577401469719f1a8866edd3dc97667aa3de287af1530aa68f50dc2508c883ecd2c9d1fe20f226ecd342b04b08f7a2cace0e531e5f49bbc12f2c')

prepare() {
  cd ndiswrapper-$pkgver
  patch --no-backup-if-mismatch -p1 -i "$srcdir"/linux-4.11.patch
  patch --no-backup-if-mismatch -p1 -i "$srcdir"/linux-4.13.patch
  patch --no-backup-if-mismatch -p1 -i "$srcdir"/linux-4.15.patch -d ndiswrapper
  patch --no-backup-if-mismatch -p1 -i "$srcdir"/linux-5.0.patch
}

build() {
  _kernver="$(cat /usr/lib/modules/$_extramodules/version)"

  cd ndiswrapper-$pkgver/ndiswrapper
  make -C driver KVERS_UNAME="$_kernver"
}

package() {
  cd ndiswrapper-$pkgver/ndiswrapper/driver
  install -Dm644 ndiswrapper.ko "$pkgdir"/usr/lib/modules/$_extramodules/ndiswrapper.ko
  gzip "$pkgdir"/usr/lib/modules/$_extramodules/ndiswrapper.ko
}
