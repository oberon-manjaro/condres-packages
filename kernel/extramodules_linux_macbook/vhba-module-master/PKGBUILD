# Maintainer: Condres OS Dev Team (x86_64) <info@codelinsoft.it>

_linuxprefix=linux-macbook
_extramodules=extramodules-Condres-macbook
pkgname=$_linuxprefix-vhba-module
_pkgname=vhba-module
pkgver=20190410
pkgrel=2
pkgdesc="Kernel module that emulates SCSI devices"
arch=('i686' 'x86_64')
url="http://cdemu.sourceforge.net/"
license=('GPL')
depends=("$_linuxprefix")
makedepends=("$_linuxprefix-headers")
provides=("$_pkgname=$pkgver")
groups=("$_linuxprefix-extramodules")
install=$_pkgname.install
source=("http://downloads.sourceforge.net/cdemu/$_pkgname-$pkgver.tar.bz2"
        '60-vhba.rules')
sha256sums=('SKIP'
            'SKIP')

prepare() {
  cd "$srcdir/$_pkgname-$pkgver"
  sed -i 's/20190302/20190410/' Makefile  # Fixup VHBA_VERSION
}

build() {
  _kernver="$(cat /usr/lib/modules/$_extramodules/version)"
  
  cd "$srcdir/$_pkgname-$pkgver"
  make -j1 KDIR=/usr/lib/modules/${_kernver}/build
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  install -D vhba.ko "$pkgdir/usr/lib/modules/$_extramodules/vhba.ko"

  sed -i "s/EXTRAMODULES=.*/EXTRAMODULES=$_extramodules/" \
    "$startdir/vhba-module.install"

  find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;

  install -Dm644 "$srcdir/60-vhba.rules" \
    "$pkgdir/usr/lib/udev/rules.d/60-$_linuxprefix-vhba.rules"
}
