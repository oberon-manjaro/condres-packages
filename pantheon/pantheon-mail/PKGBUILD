# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Riley Trautman <asonix@tamu.edu>

pkgname=pantheon-mail
pkgver=1.0.6
pkgrel=1
pkgdesc='The Pantheon Mail Client'
arch=('x86_64')
url='https://github.com/elementary/mail'
license=('GPL3')
depends=('cairo' 'gcr' 'gdk-pixbuf2' 'glib2' 'glibc' 'gmime' 'gtk3'
         'libaccounts-glib' 'libcanberra' 'libgee' 'libgsignon-glib'
         'libsecret' 'libxml2' 'pango' 'sqlite' 'webkitgtk'
         'libgranite.so')
makedepends=('cmake' 'gnome-doc-utils' 'gobject-introspection' 'granite'
             'intltool' 'vala')
source=("pantheon-mail-${pkgver}.tar.gz::https://github.com/elementary/mail/archive/${pkgver}.tar.gz")
sha256sums=('27b0b5a12ff7bfe3f96f37cb4a0602f7987ccff6000a0f1f9b115b1e8fdece46')

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  cmake ../mail-${pkgver} \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DDESKTOP_UPDATE='FALSE' \
    -DGSETTINGS_COMPILE='FALSE' \
    -DGSETTINGS_COMPILE_IN_PLACE='FALSE'
  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
