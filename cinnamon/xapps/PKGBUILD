# Maintainer: Condres OS team <info@codelinsoft.it>

pkgname=xapps
pkgver=1.2.1
pkgrel=1
pkgdesc="Common library for X-Apps project"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
depends=('libgnomekbd')
makedepends=('meson' 'gobject-introspection' 'python-gobject' 'python2-gobject' 'vala')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('daf41acb021b4dfb1b313710eea812e315a9f49e6a2cc037691ec51799f4cf26')

build() {
    mkdir -p "${srcdir}"/${pkgname}-${pkgver}/build
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    meson --prefix=/usr --buildtype=plain ..
    ninja
}

package(){
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    DESTDIR="${pkgdir}" ninja install
    # rm useless scripts
    rm -r "${pkgdir}"/usr/bin

    # byte-compile python modules since meson does not implement autotools'
    # py-compile.
    # This is kind of ugly but removes traces of the build root.
    for _python in python3 python2; do
        while read -rd '' _file; do
            _destdir="$(dirname "${_file#${pkgdir}}")"
            ${_python} -m compileall -d "${_destdir}" "${_file}"
            ${_python} -O -m compileall -d "${_destdir}" "${_file}"
        done < <(find "${pkgdir}"/usr/lib/${_python}* -name '*.py' -print0)
    done
}
