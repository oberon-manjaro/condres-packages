# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-a11y
pkgver=0.1.1
pkgrel=1
pkgdesc='Switchboard Accessibility Plug'
arch=('x86_64')
url='https://github.com/elementary/switchboard-plug-a11y'
license=('GPL3')
groups=('pantheon')
depends=('glib2' 'glibc' 'gtk3' 'libgee'
         'libgranite.so' 'libswitchboard-2.0.so')
makedepends=('cmake' 'switchboard' 'vala')
source=("switchboard-plug-a11y-${pkgver}.tar.gz::https://github.com/elementary/switchboard-plug-a11y/archive/${pkgver}.tar.gz")
sha256sums=('SKIP')

prepare() {
  cd switchboard-plug-a11y-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd switchboard-plug-a11y-${pkgver}/build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib'
  make
}

package() {
  cd switchboard-plug-a11y-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
