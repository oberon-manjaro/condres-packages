# Maintainer: Condres OS team <info@codelinsoft.it>

_gitname=powerline-2.7
_gitbranch=develop
pkgbase=python-powerline-git
pkgname=('python2-powerline-condres' 'python-powerline-condres')
pkgdesc='The ultimate statusline/prompt utility'
pkgver=2.7
pkgrel=8
epoch=1
url='https://github.com/powerline/powerline'
license=('MIT')
arch=('any')
makedepends=('git' 'python2-setuptools' 'python-setuptools' 'python2-sphinx' 'lsb-release')
install="powerline.install"
source=("https://github.com/powerline/powerline/archive/2.7.tar.gz"
        "${install}")
sha256sums=('SKIP'
            'SKIP')



build() {
	cd "${_gitname}"/docs
	make man SPHINXBUILD=sphinx-build2
}

package_generic() {
	# Fonts
	install -dm755 "${pkgdir}/etc/fonts/conf.d"
	install -Dm644 "font/PowerlineSymbols.otf" "${pkgdir}/usr/share/fonts/OTF/PowerlineSymbols.otf"
	install -Dm644 "font/10-powerline-symbols.conf" "${pkgdir}/etc/fonts/conf.avail/10-powerline-symbols.conf"
	ln -s "../conf.avail/10-powerline-symbols.conf" "${pkgdir}/etc/fonts/conf.d/10-powerline-symbols.conf"

	# Vim Plugin
	install -Dm644 "powerline/bindings/vim/plugin/powerline.vim" "${pkgdir}/usr/share/vim/vimfiles/plugin/powerline.vim"

	# Zsh Plugin
	install -Dm644 "powerline/bindings/zsh/powerline.zsh" "${pkgdir}/usr/share/zsh/site-contrib/powerline.zsh"

	# Tmux Configuration
	install -Dm644 "powerline/bindings/tmux/powerline.conf" "${pkgdir}/usr/share/tmux/powerline.conf"

	# License
	install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

	# Manpages
	install -dm755 "${pkgdir}/usr/share/man/man1/"
	install -Dm644 "docs/_build/man/"* "${pkgdir}/usr/share/man/man1/"
}

package_python2-powerline-condres() {
	depends=('python2>=2.6' 'socat')
	optdepends=('python2-psutil: improved system information'
	            'python2-pygit2: improved git support'
	            'mercurial: improved mercurial support'
	            'zsh: better shell prompt'
	            'gvim: vim compiled with Python support')
	conflicts=('python2-powerline'
	           'python-powerline-git'
	           'python-powerline'
	           'otf-powerline-symbols-git')
        

	cd "${_gitname}"
	python2 setup.py install --root="${pkgdir}" --optimize=1

	package_generic
}

package_python-powerline-condres() {
	depends=('python>=3.2' 'socat')
	optdepends=('python-psutil: improved system information'
	            'python-pygit2: improved git support'
	            'zsh: better shell prompt'
	            'gvim: vim compiled with Python support')
	conflicts=('python2-powerline'
	           'python2-powerline-git'
	           'python-powerline'
	           'otf-powerline-symbols-git')
	replaces=('python-powerline-git')
	cd "${_gitname}"
	python setup.py install --root="${pkgdir}" --optimize=1

	package_generic
}
