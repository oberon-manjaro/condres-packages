# Maintainer: Angelo Scarn√† <angelo.scarna@codelinsoft.it>

pkgname=babl
pkgver=0.1.56
pkgrel=1
pkgdesc="Dynamic, any to any, pixel format conversion library"
arch=(x86_64)
url="http://gegl.org/babl/"
license=(LGPL3)
depends=(glibc)
makedepends=(git)
source=(https://download.gimp.org/pub/babl/0.1/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('SKIP')

# Don't port to meson until babl's runtime cpu detection works there

prepare() {
  cd "$pkgname-$pkgver"
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  echo "$pkgname-$pkgver"
  make -C "$pkgname-$pkgver" check
}

package() {
  DESTDIR="$pkgdir" make -C "$pkgname-$pkgver" install
}

# vim:set sw=2 et:  
