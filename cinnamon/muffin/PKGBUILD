# Maintainer: Condres OS team <info@codelinsoft.it>

pkgname=muffin
pkgver=3.8.2
pkgrel=1
pkgdesc="Cinnamon window manager based on Mutter"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
depends=('cinnamon-desktop' 'clutter' 'gobject-introspection-runtime' 'libcanberra' 'libsm'
         'startup-notification' 'zenity')
makedepends=('intltool' 'gobject-introspection' 'gtk-doc' 'gnome-doc-utils')
options=('!emptydirs')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('SKIP')

prepare() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    PYTHON=python2 ./configure --prefix=/usr \
                               --sysconfdir=/etc \
                               --libexecdir=/usr/lib/muffin \
                               --localstatedir=/var \
                               --disable-static \
                               --disable-schemas-compile \
                               --enable-compile-warnings=minimum

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    make DESTDIR="${pkgdir}" install

    # Remove unused stuff
    make -C src DESTDIR="${pkgdir}" uninstall-binPROGRAMS uninstall-desktopfilesDATA
    make -C src/tools DESTDIR="${pkgdir}" uninstall
    make -C src/compositor/plugins DESTDIR="${pkgdir}" uninstall
    make -C doc/man DESTDIR="${pkgdir}" uninstall
}
